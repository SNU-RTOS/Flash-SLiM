#!/usr/bin/env bpftrace

BEGIN
{
    printf("Attaching USDT probes for tflite_gen application with stage-specific logging...\n");
    
    // Stage names mapping
    @stage_names[0] = "Load_Model";
    @stage_names[1] = "Build_Interpreter";
    @stage_names[2] = "Load_Tokenizer";
    @stage_names[3] = "Build_KV_Cache";
    @stage_names[4] = "Prepare_Prompt";
    @stage_names[5] = "Prepare_Signature_Runners";
    @stage_names[6] = "Prefill";
    @stage_names[7] = "Decode_Generation";
}

usdt:./output/text_generator_main_cpu:tflite_gen:logic_start
{
    @logic_start[tid] = nsecs;
    @stage_index[tid] = (uint64)arg0;
}

usdt:./output/text_generator_main_cpu:tflite_gen:logic_end
/@logic_start[tid]/
{
    $logic_duration_ms = (nsecs - @logic_start[tid]) / 1000000;
    $io_duration_ms = @io_duration_ms[tid];

    // Estimated User Time: logic 전체 시간 - io 소요시간
    $user_time_ms = $logic_duration_ms - $io_duration_ms;
    if ($user_time_ms < 0) { $user_time_ms = 0; }

    // Kernel mode I/O handling time measurements
    $read_ms = @read_block_total_us[tid] / 1000;
    $write_ms = @write_block_total_us[tid] / 1000;
    $pf_ms = @pagefault_total_us[tid] / 1000;
    $io_uring_ms = @io_uring_total_us[tid] / 1000;
    $kernel_io_ms = $read_ms + $write_ms + $pf_ms + $io_uring_ms;
    $user_io_ms = $io_duration_ms - $kernel_io_ms;

    // Total CPU runtime from sched_stat_runtime
    $cpu_runtime_ms = @cpu_runtime_us[tid] / 1000000;

    printf("\n[================ %s SUMMARY ================]\n", @stage_names[@stage_index[tid]]);
    printf(" TID                    : %d\n", tid);

    printf("-- Elapsed Time Analysis --\n");
    printf(" Wall Clock Time                 : %d ms\n", $logic_duration_ms);
    printf(" - User Logic Time (Estimated)   : %d ms\n", $user_time_ms);
    printf(" - I/O Handling elpsaed Time     : %d ms\n", $io_duration_ms);
    printf("    - User Mode (Estimated)      : %d ms\n", $user_io_ms);
    printf("    - Kernel Mode                : %d ms\n", $kernel_io_ms);
    printf("        - Read Syscall           : %d ms\n", $read_ms);
    printf("        - Write Syscall          : %d ms\n", $write_ms);
    printf("        - Page Fault             : %d ms\n", $pf_ms);
    printf("        - io_uring               : %d ms\n", $io_uring_ms);

    if ($logic_duration_ms > 0) {
        $io_ratio = ($io_duration_ms * 100) / $logic_duration_ms;
        $kernel_io_ratio = ($kernel_io_ms * 100) / $io_duration_ms;
        printf(" -- I/O Hadling Time Analysis --\n");
        printf("* I/O Ratio in Logic (%%)                 : %d\n", $io_ratio);
        printf("* Kernal Mode Ratio in I/O Handling  (%%) : %d\n", $kernel_io_ratio);
    }

    printf("-- I/O Counts --\n");
    printf(" Read Syscall Count     : %d\n", @read_count[tid]);
    printf(" Write Syscall Count    : %d\n", @write_count[tid]);
    printf(" Block I/O Count        : %d\n", @block_rq_count[tid]);
    printf(" io_uring Completion Count: %d\n", @io_uring_complete_count[tid]);

    printf("-- Total CPU Time --\n");
    printf(" Total CPU Runtime (ms) : %d\n", $cpu_runtime_ms);

    printf("-- Total I/O Time (block device) --\n");
    $block_io_ms = @block_rq_total_us[tid] / 1000;
    printf(" Total Block I/O Time   : %d ms\n", $block_io_ms);

    printf("-- Page Faults & Bytes --\n");
    printf(" Page Fault Count       : %d\n", @pagefault_count[tid]);
    printf(" Read Bytes             : %llu\n", @read_bytes[tid]);
    printf(" Write Bytes            : %llu\n", @write_bytes[tid]);
    printf(" Block Read Bytes       : %llu\n", @block_read_bytes_total[tid]);
    printf(" Block Write Bytes      : %llu\n", @block_write_bytes_total[tid]);
    printf("[===============================================]\n");

    // cleanup
    delete(@logic_start[tid]); delete(@stage_index[tid]); delete(@io_duration_ms[tid]); delete(@start[tid]);
    delete(@read_count[tid]); delete(@write_count[tid]); delete(@block_rq_count[tid]);
    delete(@io_uring_complete_count[tid]); delete(@read_block_total_us[tid]);
    delete(@write_block_total_us[tid]); delete(@block_rq_total_us[tid]);
    delete(@io_uring_total_us[tid]); delete(@read_bytes[tid]); delete(@write_bytes[tid]);
    delete(@syscall_enter[tid]); delete(@pagefault_count[tid]); delete(@pagefault_total_us[tid]);
    delete(@fault_start[tid]); delete(@io_uring_submit_start[tid]); delete(@block_read_bytes_total[tid]);
    delete(@block_write_bytes_total[tid]); delete(@cpu_runtime_us[tid]);
}

usdt:./output/text_generator_main_cpu:tflite_gen:io_start
{
    @start[tid] = nsecs;
}

usdt:./output/text_generator_main_cpu:tflite_gen:io_end
/@start[tid]/
{
    $io_duration_ms = (nsecs - @start[tid]) / 1000000;
    @io_duration_ms[tid] = $io_duration_ms;
    delete(@start[tid]);
}

tracepoint:syscalls:sys_enter_read /@start[tid]/
{
    @syscall_enter[tid] = nsecs;
    @read_count[tid] += 1;
}

tracepoint:syscalls:sys_enter_write /@start[tid]/
{
    @syscall_enter[tid] = nsecs;
    @write_count[tid] += 1;
}

tracepoint:syscalls:sys_exit_read /@syscall_enter[tid]/
{
    $block_time_us = (nsecs - @syscall_enter[tid]) / 1000;
    @read_block_total_us[tid] += $block_time_us;
    @read_bytes[tid] += args->ret >= 0 ? (uint64)args->ret : 0;
    delete(@syscall_enter[tid]);
}

tracepoint:syscalls:sys_exit_write /@syscall_enter[tid]/
{
    $block_time_us = (nsecs - @syscall_enter[tid]) / 1000;
    @write_block_total_us[tid] += $block_time_us;
    @write_bytes[tid] += args->ret >= 0 ? (uint64)args->ret : 0;
    delete(@syscall_enter[tid]);
}

kprobe:handle_mm_fault /@start[tid]/
{
    @fault_start[tid] = nsecs;
    @pagefault_count[tid] += 1;
}

kretprobe:handle_mm_fault /@fault_start[tid]/
{
    $fault_time_us = (nsecs - @fault_start[tid]) / 1000;
    @pagefault_total_us[tid] += $fault_time_us;
    delete(@fault_start[tid]);
}

tracepoint:io_uring:io_uring_submit_req /@start[tid]/
{
    @io_uring_submit_start[tid] = nsecs;
}

tracepoint:io_uring:io_uring_complete /@start[tid]/
{
    $now = nsecs;
    @io_uring_complete_count[tid] += 1;

    if (@io_uring_submit_start[tid]) {
        $io_uring_time_us = ($now - @io_uring_submit_start[tid]) / 1000;
        @io_uring_total_us[tid] += $io_uring_time_us;
        delete(@io_uring_submit_start[tid]);
    }
}

tracepoint:block:block_rq_issue /@start[tid]/
{
    @block_rq_start_nsecs[args->dev, args->sector] = nsecs;
    @block_rq_start_tid[args->dev, args->sector] = tid;
    @block_rq_start_bytes[args->dev, args->sector] = args->bytes;
    @block_rq_start_rwbs[args->dev, args->sector] = args->rwbs;
    @block_rq_count[tid] += 1;
}

tracepoint:block:block_rq_complete
{
    $start_nsecs = @block_rq_start_nsecs[args->dev, args->sector];
    $tid_orig = @block_rq_start_tid[args->dev, args->sector];
    $bytes_orig = @block_rq_start_bytes[args->dev, args->sector];
    $rwbs_orig = @block_rq_start_rwbs[args->dev, args->sector];

    if ($start_nsecs && $tid_orig) {
        $block_time_us = (nsecs - $start_nsecs) / 1000;
        @block_rq_total_us[$tid_orig] += $block_time_us;

        if ($rwbs_orig == "W") {
            @block_write_bytes_total[$tid_orig] += $bytes_orig;
        } else if ($rwbs_orig == "R" || $rwbs_orig == "RA") {
            @block_read_bytes_total[$tid_orig] += $bytes_orig;
        }

        delete(@block_rq_start_nsecs[args->dev, args->sector]);
        delete(@block_rq_start_tid[args->dev, args->sector]);
        delete(@block_rq_start_bytes[args->dev, args->sector]);
        delete(@block_rq_start_rwbs[args->dev, args->sector]);
    }
}

tracepoint:sched:sched_stat_runtime /@start[tid]/
{
    @cpu_runtime_us[tid] += args->runtime;
}

END
{
    printf("Cleaning up global tracking maps...\n");
    clear(@stage_names);
    clear(@logic_start);
    clear(@stage_index);
    clear(@start);
    clear(@io_duration_ms);
    clear(@syscall_enter);
    clear(@fault_start);
    clear(@io_uring_submit_start);
    clear(@block_rq_start_nsecs);
    clear(@block_rq_start_tid);
    clear(@block_rq_start_bytes);
    clear(@block_rq_start_rwbs);
    clear(@read_block_total_us);
    clear(@write_block_total_us);
    clear(@pagefault_total_us);
    clear(@io_uring_total_us);
    clear(@cpu_runtime_us);
    clear(@read_count);
    clear(@write_count);
    clear(@block_rq_count);
    clear(@io_uring_complete_count);
    clear(@block_rq_total_us);
    clear(@pagefault_count);
    clear(@read_bytes);
    clear(@write_bytes);
    clear(@block_read_bytes_total);
    clear(@block_write_bytes_total);
    printf("Cleanup complete.\n");
}
